############################################################## 
############################################################## 
############################################################## Old Tokens
############################################################## 
############################################################## 

mse version: 0.3.5
game: magic
short name: Tokens
full name: Old style
icon: card-sample.png
position hint: 107
version: 2007-09-23

card width: 375
card height: 523
card dpi: 150

############################################################## Extra scripts
## COPY/PASTE from magic-old.mse-style/style

init script:
	# There is no land template
	land_template := { "c{type}.jpg" }
	
	# Use the normal tap symbol
	mana_t := {
		if      styling.tap_symbol == "old"        then "old"
		else if styling.tap_symbol == "diagonal T" then "older"
		else                                            "new"
	}
	# Use guild mana symbols?
	guild_mana := { styling.use_guild_mana_symbols }
	
	# Only white cards use a black font
	font_colors_white := { input != "w" }
	
	color_combination := {
		# The base hybrid, without the outer frame blended over it
		base := base_hybrid[shape][color_count]()
		
		# Put a frame around it?
		if land and not colored_lands then
			masked_blend(
				mask:  "multicolor_blend_{type}.png",
				dark:  land_template("c"),
				light: base,
			)
		else if multi then 
			masked_blend(
				mask: ( if styling.type_of_gradient_multicolor == "full card" then "multicolor_blend_{type}.png"
					else if styling.type_of_gradient_multicolor == "trim only" then "multicolor_blend_{type}2.png"
					else "multicolor_blend_{type}.png" ),
				dark:  template("m"),
				light: base,
			)
		else if artifact then 
			masked_blend(
				mask:  "multicolor_blend_{type}.png",
				dark:  template("a"),
				light: base,
			)
		else if color_count > 1 and mask_hybrid_with_land() then
			masked_blend(
				mask:  "hybrid_blend_{type}.png",
				dark:  land_template("c"),
				light: base,
			)
		else base
	}
	
	card_shape := { "token" }

############################################################## Set info fields
set info style:
	symbol:
		variation:
			name: olduncommon
			border radius: 0.10
			fill type: linear gradient
			fill color 1:   rgb(224,224,224)
			fill color 2:   rgb(84, 84, 84)
			border color 1: rgb(255,255,255)
			border color 2: rgb(255,255,255)
		variation:
			name: oldrare
			border radius: 0.10
			fill type: linear gradient
			fill color 1:   rgb(214,196,94)
			fill color 2:   rgb(95, 84, 40)
			border color 1: rgb(255,255,255)
			border color 2: rgb(255,255,255)

############################################################## Extra style options

styling field:
	type: boolean
	name: use guild mana symbols
	description: Use the Ravnica guild symbols instead of the official half/half circles for hybrid mana.
	initial: no
styling field:
	type: boolean
	name: popup mana symbols
	description: Use the symbols of the casting cost line inside the text box as well
	initial: no
styling field:
	type: choice
	name: type of gradient multicolor
	description: What parts of the card should be blended for gradient multicolor?
	choice: full card
	choice: trim only
	initial: full card
styling field:
	type: choice
	name: tap symbol
	description: What tap symbol should be used on cards?
	initial: old
	choice: modern
	choice: old
	choice: diagonal T
styling field:
	type: choice
	name: center text
	description: When to center text (short text only means only on one-line cards with no flavor text)
	choice: always
	choice: short text only
	choice: never
	initial: short text only
styling field:
	type: boolean
	name: colored rarities
	description: Should rarity symbols be different colors, such as gold, silver, and purple?
	initial: yes

styling style:
	use guild mana symbols:
		choice images:
			yes: /magic-mana-small.mse-symbol-font/mana_guild_rg.png
			no:  /magic-mana-small.mse-symbol-font/mana_rg.png
	popup mana symbols:
		choice images:
			yes: /magic-mana-large.mse-symbol-font/mana_w.png
			no:  /magic-mana-small.mse-symbol-font/mana_w.png
	tap symbol:
		render style: both
		choice images:
			modern:     /magic-mana-large.mse-symbol-font/mana_t.png
			old:        /magic-mana-large.mse-symbol-font/mana_t_old.png
			diagonal T: /magic-mana-large.mse-symbol-font/mana_t_older.png

############################################################## Card fields
card style:
	############################# Background stuff
	border color:
		left: 0
		top : 0
		width:	375
		height:	523
		radius: 18
		left width:   17
		right width:  17
		top width:    17
		bottom width: 18
		z index: -1
	card color:
		left:	0
		top:	0
		width:	375
		height:	523
		z index: -2
		render style: image
		popup style: in place
		image: { card_background() }

	############################# Name line
	name:
		left: 40
		top : 23
		width: 295
		height: 25
		alignment: middle center
		padding bottom: 0
		z index: 1
		font:
			name: MPlantin
			size: 17
			color: rgb(255,255,255)
			shadow color: rgb(0,0,0)
			shadow displacement x: 1
			shadow displacement y: 1

	############################# Image
	image:
		left:	44
		top:	58
		width:	286
		height:	270
		z index: 1

	############################# Card type

	type:
		left: 58
		top : 348
		width: { 262 - max(22,card_style.rarity.content_width) }
		height: 18
		alignment: top left shrink-overflow
		z index: 1
		font:
			name: MPlantin
			size: 12
			color: rgb(255,255,255)
			shadow color: rgb(0,0,0)
			shadow displacement x: 1
			shadow displacement y: 1
			separator color: rgb(192,192,192)

	rarity:
		right: 320
		top : 347
		width: 46
		height: 23
		z index: 1
		alignment: middle right
		render style: image
		choice images:
			# Images based on the set symbol
			basic land: script: symbol_variation(symbol: set.symbol, variation: "common")
			common:     script: symbol_variation(symbol: set.symbol, variation: "common")
			uncommon:
				script:
					if styling.colored_rarities
					then symbol_variation(symbol: set.symbol, variation: "olduncommon")
					else symbol_variation(symbol: set.symbol, variation: "common")
			rare:
				script:
					if styling.colored_rarities
					then symbol_variation(symbol: set.symbol, variation: "oldrare")
					else symbol_variation(symbol: set.symbol, variation: "common")
			special:
				script:
					if styling.colored_rarities
					then symbol_variation(symbol: set.symbol, variation: "special")
					else symbol_variation(symbol: set.symbol, variation: "common")

	############################# Text box
	text:
		left: 63
		top : 378
		width: 246
		height: 67
		font:
			name: MPlantin
			italic name: MPlantin-Italic
			size: 13
			scale down to: 6
			color: rgb(0,0,0)
		symbol font:
			name:
				script:
					if styling.popup_mana_symbols
					then "magic-mana-large"
					else "magic-mana-small"
			size: 13
		alignment:
			script:
				if (styling.center_text == "short text only" and
				   card_style.text.content_lines <= 2 and
				   not contains(match:",", card.rule_text) and
				   not contains(match: ".", card.rule_text) and
				   not contains(match: ";", card.rule_text) and
				   not contains(match: ":", card.rule_text) and
				   not contains(match: "!", card.rule_text) and
				   not contains(match: "?", card.rule_text) and
				   card.flavor_text == "<i-flavor></i-flavor>") or
				   styling.center_text == "always"
				then "middle center shrink-overflow"
				else "middle left shrink-overflow"
		z index: 2
		padding left:   2
		padding top:    2
		padding right:  2
		padding bottom: 2
		line height hard: 1.2
		line height line: 1.5
		line height soft: 0.9

	############################# PT
	pt:
		z index: 4
		left:	283
		top:	458
		width:	45
		height:	30
		alignment: right top
		font:
			name: Mplantin
			size: 18
			weight: bold
			color: rgb(255,255,255)
			shadow color: rgb(0,0,0)
			shadow displacement x: 2
			shadow displacement y: 2
			separator color: rgb(255,50,50)

	############################# Card sorting / numbering
	############################# Copyright stuff

	illustrator:
		left: 97
		top : 459
		width: 180
		height: 15
		z index: 1
		alignment: middle center
		font:
			name: Mplantin
			size: 9
			color: rgb(255,255,255)
			shadow color: rgb(0,0,0)
			shadow displacement x: 1
			shadow displacement y: 1

	copyright line:
		left: 97
		top : 472
		width: 180
		height: 12
		z index: 1
		alignment: middle center
		font:
			name: MPlantin
			size: 8
			color: { font_color(card.card_color) }
